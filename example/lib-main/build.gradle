apply plugin: 'com.android.library'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {

        flavor1 {

        }

        flavor2 {

        }
    }

    packagingOptions {
        exclude 'lib/x86/*.so'
        exclude 'lib/x86_64/*.so'
        exclude 'lib/armeabi/*.so'
        exclude 'lib/arm64-v8a/*.so'
    }
}

afterEvaluate {
    // for app test, copy the final aar to application libs
    android.libraryVariants.all { variant ->
        File outputFile = variant.outputs.first().outputFile
        tasks.named("assemble${variant.name.capitalize()}").configure {
            doLast {
                copy {
                    from outputFile
                    into "../app/libs"
                    rename outputFile.name, "fat-aar-final.aar"
                }
            }
        }
    }
}

fataar {
    /**
     * Plan A: using bytecode patching to process the merging problem of R files
     * Plan B: generate sub module's R class to process the merging problem of R files
     * if transformR is true, use Plan A, else use Plan B.
     * In the future, Plan B maybe deprecated
     * Default value is true
     * @since 1.3.0
     */
    transformR = true

    /**
     * If transitive is true, local jar module and remote library's dependencies will be embed. (local aar module does not support)
     * If transitive is false, just embed first level dependency
     * Default value is false
     * @since 1.3.0
     */
    transitive = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    // java dependency
    embed project(path: ':lib-java')
    // aar dependency
    embed project(path: ':lib-aar')
    // aar dependency
    embed project(path: ':lib-aar2')
    // local full aar dependency, just build in flavor1
    flavor1Embed project(path: ':lib-aar-local')
    // local full aar dependency, just build in debug
    debugEmbed (name:'lib-aar-local2', ext:'aar')
    // remote jar dependency
    embed 'com.google.guava:guava:20.0'
    // remote aar dependency
    embed 'com.facebook.fresco:fresco:1.11.0'
    // remote aar dependency
    embed ('com.github.bumptech.glide:glide:4.11.0') {
        transitive = false
    }
    // don't want to embed in
    implementation('androidx.appcompat:appcompat:1.2.0')
}

